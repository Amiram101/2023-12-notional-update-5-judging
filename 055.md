Short Candy Salmon

high

# wfCashLogic.sol returned eth is locked in the contract

## Summary
This update has modified `_sendTokensToReceiver()` to only calculate the difference of WETH, no longer using the difference of `eth`. 
However, in many places, `Notional` still returns `eth`, causing this part of eth to be locked in the contract.

## Vulnerability Detail

Currently, `_sendTokensToReceiver()` has been modified to only calculate the difference of WETH.

```solidity
    function _sendTokensToReceiver(
        IERC20 token,
        address receiver,
        bool isETH,
        uint256 balanceBefore
    ) private returns (uint256 tokensTransferred) {
        uint256 balanceAfter = isETH ? WETH.balanceOf(address(this)) : token.balanceOf(address(this));
        tokensTransferred = balanceAfter - balanceBefore;

        if (isETH) {
            // No need to use safeTransfer for WETH since it is known to be compatible
            IERC20(address(WETH)).transfer(receiver, tokensTransferred);
        } else if (tokensTransferred > 0) {
            token.safeTransfer(receiver, tokensTransferred);
        }
    }
```

But the methods `NotionalV2.depositUnderlyingToken()` and `NotionalV2.batchBalanceAndTradeAction()` still return `eth`.

```solidity
    function _mintInternal(
        uint256 depositAmountExternal,
        uint88 fCashAmount,
        address receiver,
        uint32 minImpliedRate,
        uint256 maxFCash
    ) internal nonReentrant {
...
        if (maxFCash < fCashAmount) {
            // NOTE: lending at zero
            uint256 fCashAmountExternal = fCashAmount * precision / uint256(Constants.INTERNAL_TOKEN_PRECISION);
            require(fCashAmountExternal <= depositAmountExternal);

            // NOTE: Residual (depositAmountExternal - fCashAmountExternal) will be transferred
            // back to the account
@>          NotionalV2.depositUnderlyingToken{value: msgValue}(address(this), currencyId, fCashAmountExternal);
        } else if (isETH || hasTransferFee || getCashBalance() > 0) {
            _lendLegacy(currencyId, depositAmountExternal, fCashAmount, minImpliedRate, msgValue, isETH);
        } else {
...

    function _lendLegacy(
        uint16 currencyId,
        uint256 depositAmountExternal,
        uint88 fCashAmount,
        uint32 minImpliedRate,
        uint256 msgValue,
        bool isETH
    ) internal {
....
       // Notional will return any residual ETH as the native token. When we _sendTokensToReceiver those
        // native ETH tokens will be wrapped back to WETH.
@>      NotionalV2.batchBalanceAndTradeAction{value: msgValue}(address(this), action);

        uint256 postTradeCash = getCashBalance();


```

In this way, the returned eth is locked in the contract.

## Impact

The returned eth is locked in the contract.

## Code Snippet
https://github.com/sherlock-audit/2023-12-notional-update-5/blob/main/wrapped-fcash/contracts/wfCashLogic.sol#L337C2-L337C2
## Tool used

Manual Review

## Recommendation

First convert eth to WETH, then perform WETH difference calculation.

```diff
    function _sendTokensToReceiver(
        IERC20 token,
        address receiver,
        bool isETH,
        uint256 balanceBefore
    ) private returns (uint256 tokensTransferred) {
+      if (isETH && address(this).balance > 0 ) WETH.deposit{value: address(this).balance}();

        uint256 balanceAfter = isETH ? WETH.balanceOf(address(this)) : token.balanceOf(address(this));
        tokensTransferred = balanceAfter - balanceBefore;

        if (isETH) {
            // No need to use safeTransfer for WETH since it is known to be compatible
            IERC20(address(WETH)).transfer(receiver, tokensTransferred);
        } else if (tokensTransferred > 0) {
            token.safeTransfer(receiver, tokensTransferred);
        }
    }
```
