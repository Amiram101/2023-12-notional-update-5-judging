Short Candy Salmon

medium

# mintViaUnderlying() minImpliedRate may be invalid

## Summary
When `mintViaUnderlying()` we can specify `minImpliedRate` to limit the minimum rate. 
However, when `maxFCash < fCashAmount`, this limit is ignored. 
This may lead to results completely different from the user's expectations.

## Vulnerability Detail

When calling `mintViaUnderlying()`, we can specify `minImpliedRate` to avoid a rate that is too small.

```solidity
    function mintViaUnderlying(
        uint256 depositAmountExternal,
        uint88 fCashAmount,
        address receiver,
        uint32 minImpliedRate
    ) external override {
        (/* */, uint256 maxFCash) = getTotalFCashAvailable();
        _mintInternal(depositAmountExternal, fCashAmount, receiver, minImpliedRate, maxFCash);
    }

    function _mintInternal(
        uint256 depositAmountExternal,
        uint88 fCashAmount,
        address receiver,
        uint32 minImpliedRate,
        uint256 maxFCash
    ) internal nonReentrant {
...

        if (maxFCash < fCashAmount) {
            // NOTE: lending at zero
            uint256 fCashAmountExternal = fCashAmount * precision / uint256(Constants.INTERNAL_TOKEN_PRECISION);
            require(fCashAmountExternal <= depositAmountExternal);

            // NOTE: Residual (depositAmountExternal - fCashAmountExternal) will be transferred
            // back to the account
            NotionalV2.depositUnderlyingToken{value: msgValue}(address(this), currencyId, fCashAmountExternal);
        } else if (isETH || hasTransferFee || getCashBalance() > 0) {
@>          _lendLegacy(currencyId, depositAmountExternal, fCashAmount, minImpliedRate, msgValue, isETH);
        } else {
            // Executes a lending action on Notional. Since this lending action uses an existing cash balance
            // prior to pulling payment, we cannot use it if there is a cash balance on the wrapper contract,
            // it will cause existing cash balances to be minted into fCash and create a shortfall. In normal
            // conditions, this method is more gas efficient.
            BatchLend[] memory action = EncodeDecode.encodeLendTrade(
                currencyId,
                getMarketIndex(),
                fCashAmount,
@>              minImpliedRate
            );
            NotionalV2.batchLend(address(this), action);
        }

        // Mints ERC20 tokens for the receiver
        _mint(receiver, fCashAmount);

        // Residual tokens will be sent back to msg.sender, not the receiver. The msg.sender
        // was used to transfer tokens in and these are any residual tokens left that were not
        // lent out. Sending these tokens back to the receiver risks them getting locked on a
        // contract that does not have the capability to transfer them off
        _sendTokensToReceiver(token, msg.sender, isETH, balanceBefore);
    }

```

From the above code, we can see that the first branch of the `if` statement, `maxFCash < fCashAmount`, does not validate `minImpliedRate`.

This leads to the assumption that the user submits the transaction : `mintViaUnderlying(minImpliedRate=1%)`. 
But when the transaction is executed, due to insufficient `fCash`, it enters the first branch, lending at zero. 
At this time, it should fail (rate=0 < 1%)
This is already below the user's expected rate.

## Impact

The `minImpliedRate` lower than the user's expectation can still be successfully executed, which harms the user's interests.

## Code Snippet

https://github.com/sherlock-audit/2023-12-notional-update-5/blob/main/wrapped-fcash/contracts/wfCashLogic.sol#L60-L68

## Tool used

Manual Review

## Recommendation
```diff
    function _mintInternal(
        uint256 depositAmountExternal,
        uint88 fCashAmount,
        address receiver,
        uint32 minImpliedRate,
        uint256 maxFCash
    ) internal nonReentrant {
...
        if (maxFCash < fCashAmount) {
+          require(minImpliedRate==0,"invalid minImpliedRate);
            // NOTE: lending at zero
            uint256 fCashAmountExternal = fCashAmount * precision / uint256(Constants.INTERNAL_TOKEN_PRECISION);
            require(fCashAmountExternal <= depositAmountExternal);

            // NOTE: Residual (depositAmountExternal - fCashAmountExternal) will be transferred
            // back to the account
            NotionalV2.depositUnderlyingToken{value: msgValue}(address(this), currencyId, fCashAmountExternal);
        } else if (isETH || hasTransferFee || getCashBalance() > 0) {
            _lendLegacy(currencyId, depositAmountExternal, fCashAmount, minImpliedRate, msgValue, isETH);
        } else {
```

